load("@bazel-orfs//:sweep.bzl", "orfs_sweep")
load("//util:plot_congestion.bzl", "plot_congestion")

# Format densities, rounding to 2 decimal places.
SWEEPS = {
    "PLACE_DENSITY": [str(0.60 + x * 0.01 + 0.005)[:4] for x in range(20)],
    "CORE_UTILIZATION": [str(40 + x * 5) for x in range(4)],
}

SWEEP = "PLACE_DENSITY"

orfs_sweep(
    name = "ethmac_lvt",
    arguments = {
        # Faster builds
        "SKIP_INCREMENTAL_REPAIR": "1",
        "GPL_TIMING_DRIVEN": "0",
        # Various
        "SDC_FILE": "$(location :constraint.sdc)",
        "ABC_AREA": "1",
        "CORE_UTILIZATION": "40",
        "CORE_ASPECT_RATIO": "1",
        "CORE_MARGIN": "2",
        "PLACE_DENSITY": "0.60",
        "ASAP7_USELVT": "1",
        "RECOVER_POWER": "1",
        "ADDITIONAL_LIBS": "$(LIB_DIR)/asap7sc7p5t_AO_RVT_FF_nldm_211120.lib.gz \
                                $(LIB_DIR)/asap7sc7p5t_INVBUF_RVT_FF_nldm_220122.lib.gz \
			        $(LIB_DIR)/asap7sc7p5t_OA_RVT_FF_nldm_211120.lib.gz \
			        $(LIB_DIR)/asap7sc7p5t_SIMPLE_RVT_FF_nldm_211120.lib.gz \
			        $(LIB_DIR)/asap7sc7p5t_SEQ_RVT_FF_nldm_220123.lib",
        "ADDITIONAL_GDS": "$(PLATFORM_DIR)/gds/asap7sc7p5t_28_R_220121a.gds",
        "ADDITIONAL_LEFS": "$(PLATFORM_DIR)/lef/asap7sc7p5t_28_R_1x_220121a.lef",
    },
    other_variants = {"base": {}},
    sources = {
        "SDC_FILE": [":constraint.sdc"],
    },
    sweep = {
        value: {
            "arguments": {
                SWEEP: value,
            },
            "previous_stage": {
                "floorplan": "ethmac_lvt_synth",
            },
        }
        for value in SWEEPS[SWEEP]
    },
    top = "ethmac",
    verilog_files = ["//designs/src/ethmac_lvt:verilog"],
)

plot_congestion(
    name = "plot",
    srcs = [":ethmac_lvt_{value}_grt".format(value = value) for value in SWEEPS[SWEEP]],
    argument = SWEEP,
    values = SWEEPS[SWEEP],
)
