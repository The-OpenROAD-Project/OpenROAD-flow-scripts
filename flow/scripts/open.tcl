# This script is used to launch the OpenROAD user ineterface
# console as well as GUI mode. The user interface will remain
# go from batch to interactive mode after this script has
# been executed, whether it failed or not. For instance,
# a failed global route run can be loaded to examine the
# DRC errors.
#
# However, this script is also used to load the design and timing
# in automation scripts, hence the script should fail upon
# errors, not continue.
source $::env(SCRIPTS_DIR)/util.tcl
# Read liberty files
source $::env(SCRIPTS_DIR)/read_liberty.tcl

# Read def
if {[env_var_exists_and_non_empty DEF_FILE]} {
    # Read lef
    read_lef $::env(TECH_LEF)
    read_lef $::env(SC_LEF)
    if {[env_var_exists_and_non_empty ADDITIONAL_LEFS]} {
      foreach lef $::env(ADDITIONAL_LEFS) {
        read_lef $lef
      }
    }
    set input_file $::env(DEF_FILE)
    read_def $input_file
} else {
    set input_file $::env(ODB_FILE)
    read_db $input_file
}

proc read_timing {input_file} {
  set result [find_sdc_file $input_file]
  set design_stage [lindex $result 0]
  set sdc_file [lindex $result 1]

  if {$sdc_file == ""} {
    set sdc_file $::env(SDC_FILE)
  }
  read_sdc $sdc_file
  if [file exists $::env(PLATFORM_DIR)/derate.tcl] {
    source $::env(PLATFORM_DIR)/derate.tcl
  }
  
  source $::env(PLATFORM_DIR)/setRC.tcl
  if {$design_stage >= 4} {
    # CTS has run, so propagate clocks
    set_propagated_clock [all_clocks]
  }
  
  if {$design_stage >= 6 && [file exist $::env(RESULTS_DIR)/6_final.spef]} {
    log_cmd read_spef $::env(RESULTS_DIR)/6_final.spef
  } elseif {$design_stage >= 5} {
    if { [grt::have_routes] } {
      log_cmd estimate_parasitics -global_routing
    } else {
      error "No global routing results available,\
      skipping estimate_parasitics,\
      load $::global_route_congestion_report for details"
    }
  } elseif {$design_stage >= 3} {
    log_cmd estimate_parasitics -placement
  }

  fast_route

  puts "Populating timing paths..."
  # Warm up OpenSTA, so clicking on timing related buttons reacts faster
  set _tmp [find_timing_paths]
}

if {[env_var_equals GUI_TIMING 1]} {
  puts "GUI_TIMING=1 reading timing, takes a little while for large designs..."
  read_timing $input_file
}

if {[env_var_equals GUI_SHOW 1]} {
  # Show the GUI when it is ready; it is unresponsive(with modal requesters
  # saying it is unresponsive) until everything is loaded
  gui::show
}
